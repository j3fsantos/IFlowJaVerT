pred Cell_o (l, prop, v) :
	types(l : $$object_type, prop : $$string_type) * ((l, prop) -> v);

spec o__getOwnProperty (l, prop)
	[[ Cell_o (l, prop, None) ]]
	[[ Cell_o (l, prop, None) * (ret == $$undefined) ]]
	normal;
	
	[[ Cell_o (l, prop, #d) * (!(#d == None)) ]]
	[[ Cell_o (l, prop, #d) * (!(#d == None)) * types(#d : $$list_type) * (ret == #d) ]]
	normal
proc o__getOwnProperty (l, prop) {
            x := hasField(l, prop);		
            goto [x] hp nhp; 			(* Does the field exist? *)
            
    nhp:    xret := $$undefined;		(* Field doesn't exist; return undefined *)
            goto rlab;					
            
    hp:     xret := [l, prop];			(* Field exists; return the descriptor *)
    
    rlab:   skip
}
with
{
    ret: 	xret, rlab;
};

pred CommonMethods (l) :
	((l, "@getProperty") -> "o__getProperty");

pred SpecificMethods_o (l) :
	((l, "@getOwnProperty") -> "o__getOwnProperty");
pred SpecificMethods_s (l) :
	((l, "@getOwnProperty") -> "s__getOwnProperty");

pred ProtoLookup (l, prop, d) :
	SpecificMethods_o (l) * Cell_o (l, prop, None) * Cell_o (l, "@proto", $$null) * (d == $$undefined),
	SpecificMethods_o (l) * Cell_o (l, prop, d) * (!(d == None)),
	SpecificMethods_o (l) * Cell_o (l, prop, None) * Cell_o (l, "@proto", #lp)  * (!(#lp == $$null)) * CommonMethods(#lp) * ProtoLookup(#lp, prop, d);

spec o__getProperty (l, prop)
	[[ ProtoLookup (l, prop, #d) ]]
	[[ ProtoLookup (l, prop, #d) * ret == #d ]]
	normal
proc o__getProperty (l, prop) {
			[* unfold ProtoLookup (l, prop, #d) *]
			gop := [l, "@getOwnProperty"];
			xret := gop (l, prop) with elab;
			goto [xret = $$undefined] next rlab;
			
	next:	proto := [l, "@proto"];
			goto [proto = $$null] rlab call;
			
	call:	gp := [proto, "@getProperty"];
			xret := gp (proto, prop) with elab;
			[* fold ProtoLookup (l, prop, #d) *]
    rlab:   skip;
    elab:	skip
}
with 
{
    ret: 	xret, rlab;
    err:	xret, elab;
};
