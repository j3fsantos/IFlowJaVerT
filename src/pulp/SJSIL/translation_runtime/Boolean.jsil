import Internals;

(* ******************* *) 
(* THE BOOLEAN LIBRARY *)
(* ******************* *) 

(* *** CALL *** *)

proc Boolean_call (xsc, vthis, v) {
	rlab:	xret := "i__toBoolean" (v)
}
with
{
	ret: 	xret, rlab;
};

(* *** CONSTRUCT *** *)

proc Boolean_construct (xsc, vthis, v) {
			pv := "i__toBoolean" (v);
			
			xret := new ();
			xret := "create_default_object" (xret, $lbool_proto, "Boolean", $$t);
			
			[xret, "@primitiveValue"] := pv;
						
	rlab:	skip
}
with
{
	ret: 	xret, rlab;
};

(* *** TOSTRING *** *)

proc BP_toString (xsc, vthis) {
			goto [typeOf(vthis) = $$boolean_type] isb notb;
	
	isb:	xret := vthis;
			goto test;
			
	notb:	goto [typeOf(vthis) = $$object_type] class throw;
	class:	c := [vthis, "@class"];
			goto [c = "Boolean"] b throw;
	b:		xret := [vthis, "@primitiveValue"];
	test:		goto [xret = $$t] rt rf;
	
	rt:		xret := "true";
			goto rlab;
			
	rf:		xret := "false";

	rlab:	skip;
	
	throw:	xret := "TypeError" ();
	elab: 	skip
}
with
{
	ret: 	xret, rlab;
	err: 	xret, elab;
};

(* *** VALUEOF *** *)

proc BP_valueOf (xsc, vthis) {
			goto [typeOf(vthis) = $$boolean_type] isb notb;
	
	isb:	xret := vthis;
			goto rlab;
			
	notb:	goto [typeOf(vthis) = $$object_type] class throw;
	class:	c := [vthis, "@class"];
			goto [c = "Boolean"] b throw;
	b:		xret := [vthis, "@primitiveValue"];

	rlab:	skip;
	
	throw:	xret := "TypeError" ();
	elab: 	skip
}
with
{
	ret: 	xret, rlab;
	err: 	xret, elab;
}
