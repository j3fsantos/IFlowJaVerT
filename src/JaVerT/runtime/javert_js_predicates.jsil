(*************************
 **** GENERAL OBJECTS ****
 *************************)

pred JSObject (l:Obj, md:Obj) :
    MetaData(l, md) * Extensible(l, true) *
	((md, "@proto")      -> $lobj_proto) *
	((md, "@class")      -> "Object") *
	((md, "@extensible") -> true) *
	MetaData(md, null) * Extensible(md, true);

pred JSObjWithProto (l:Obj, proto, md:Obj) :
    MetaData(l, md) * Extensible(l, true) *
	((md, "@proto")      -> proto) *
	((md, "@class")      -> "Object") *
	((md, "@extensible") -> true) *
	MetaData(md, null) * Extensible(md, true);

pred JSObjGeneral (l:Obj, proto, class:Str, ext:Bool, md:Obj) :
    MetaData(l, md) * Extensible(l, true) *
	((md, "@proto")      -> proto) *
	((md, "@class")      -> class) *
	((md, "@extensible") -> ext) *
	MetaData(md, null) * Extensible(md, true);
	
pred JSObjGeneralWeak (l:Obj, proto, class, ext, md:Obj) :
    MetaData(l, md) * Extensible(l, true) *
	((md, "@proto")      -> proto) *
	((md, "@class")      -> class) *
	((md, "@extensible") -> ext) *
	MetaData(md, null) * Extensible(md, true);
	
pred EmptyExtensibleObject(l:Obj, md:Obj) :
	empty_fields(l  : -{}-) * MetaData(l,  md) *   Extensible(l, true) *
	empty_fields(md : -{}-) * MetaData(md, null) * Extensible(md, true);

(************************
 ***** INITIAL HEAP *****
 ************************)

pred initialHeapPre () :
    EmptyExtensibleObject ($lg, $lg_md) *
    EmptyExtensibleObject ($lobj_proto, $lobj_proto_md) * 
    EmptyExtensibleObject ($lfun_proto, $lfun_proto_md) * 
    EmptyExtensibleObject ($lerr, $lerr_md) *
    EmptyExtensibleObject ($lerr_proto, $lerr_proto_md) * 
    EmptyExtensibleObject ($lop_hasOwnProperty, $lop_hasOwnProperty_md);
    
pred initialHeapPost (globals) :
	types(globals:Set) *
	
	JSObject ($lg, $lg_md) *
	(($lg, "Error") -> {{ "d", $lerr, true, false, true }}) *
    empty_fields ($lg    : -u- (-{ "Error" }-, globals) ) *
    empty_fields ($lg_md : -{ "@class", "@proto", "@extensible" }-) *
    
    JSObjWithProto ($lobj_proto, null, $lobj_proto_md) *
    (($lobj_proto, "hasOwnProperty") -> {{ "d", $lop_hasOwnProperty, true, false, true }}) *
    empty_fields ($lobj_proto    : -{ "hasOwnProperty" }-) *
    empty_fields ($lobj_proto_md : -{ "@class", "@proto", "@extensible" }-) *

    JSObjGeneral ($lop_hasOwnProperty, $lfun_proto, "Function", true, $lop_hasOwnProperty_md) *
    (($lop_hasOwnProperty, "length") -> {{ "d", 1, false, false, false }}) *
    empty_fields($lop_hasOwnProperty : -{ "length" }-) *
    (($lop_hasOwnProperty_md, "@scope") -> empty) * (($lop_hasOwnProperty_md, "@call") -> "OP_hasOwnProperty") *
    empty_fields($lop_hasOwnProperty_md : -{ "@proto", "@class", "@extensible", "@scope", "@call" }-) *
    
    JSObjGeneral ($lfun_proto, $lobj_proto, "Function", true, $lfun_proto_md) *
    (($lfun_proto, "length") -> {{ "d", 0, false, false, false }}) *
    empty_fields($lfun_proto : -{ "length" }-) *
    (($lfun_proto_md, "@scope") -> empty) * (($lfun_proto_md, "@call") -> "FP_default") *
    empty_fields($lfun_proto_md : -{ "@proto", "@class", "@extensible", "@scope", "@call" }-) *
      
    JSObjGeneral ($lerr, $lfun_proto, "Function", true, $lerr_md) *
    (($lerr, "length") -> {{ "d", 1, false, false, false }}) *
    (($lerr, "prototype") -> {{ "d", $lerr_proto, false, false, false }}) *
    empty_fields($lerr : -{ "length", "prototype" }-) *
    (($lerr_md, "@scope") -> empty) * (($lerr_md, "@call") -> "Error_call") * (($lerr_md, "@construct") -> "Error_construct") *
    empty_fields($lerr_md : -{ "@proto", "@class", "@extensible", "@scope", "@call", "@construct" }-) *
    
    JSObjGeneral ($lerr_proto, $lobj_proto, "Error", true, $lerr_proto_md) * 
    (($lerr_proto, "message") -> {{ "d", "", true, false, true }}) *
    empty_fields($lerr_proto : -{ "message" }-) *
    empty_fields($lerr_proto_md : -{ "@proto", "@class", "@extensible" }-);

(**************************
 **** FUNCTION OBJECTS ****
 **************************)

pred FunctionObject (l:Obj, fid:Str, sc:List, len:Num, proto:Obj, md:Obj) : 
	((l, "length")     -> {{ "d", len, false, false, false }}) *
	((l, "arguments")  -> {{ "a", $lthrow_type_error, $lthrow_type_error, false, false }}) *
	((l, "caller")     -> {{ "a", $lthrow_type_error, $lthrow_type_error, false, false }}) *
	((l, "prototype")  -> {{ "d", proto, true, false, false }}) *
	MetaData(l, md) * Extensible (l, true) *
	((md, "@proto")      -> $lfun_proto) * 
	((md, "@class")      -> "Function") *
	((md, "@extensible") -> true) * 
	((md, "@call")       -> fid) * 
	((md, "@construct")  -> fid) *
	((md, "@scope")      -> sc) *
	MetaData(md, null) * Extensible(md, true);

(***********************
 **** ERROR OBJECTS ****
 ***********************)
 
pred ErrorObject (l : Obj, pr : Obj, md) :
	JSObjGeneral(l, pr, "Error", true, md);

pred ErrorObjectWithMessage (l : Obj, m : Str, md) :
  MetaData(l, md) * Extensible(l, true) *
  ((md, "@proto") -> $lerr_proto) * ((md, "@class") -> "Error") * ((md, "@extensible") -> true) *
  ((md, "message") -> {{"d", m, true, false, true}}) * 
  MetaData(md, null) * Extensible(md, true);

pred isTypeError(l : Obj, md) :
    ErrorObject (l, $lterr_proto, md);

pred isSyntaxError(l : Obj, md) :
    ErrorObject (l, $lserr_proto, md);

pred isReferenceError(l : Obj, md) :
    ErrorObject (l, $lrferr_proto, md);

(********************
 **** PROPERTIES ****
 ********************)

pred DataProp (l : Obj, prop : Str, v) :
	((l, prop) -> {{ "d", v, true, true, true }}) *
	(! (v == empty));
	
pred DataPropGen (l : Obj, prop : Str, v, writ : Bool, enum : Bool, conf : Bool) : 
	((l, prop) -> {{ "d", v, writ, enum, conf }}) *
	(! (v == empty));
	
pred DataPropConst (l : Obj, prop : Str, v) :
	((l, prop) -> {{ "d", v, false, #e, false }}) *
	types(#e:Bool) * (! (v == empty));

(*********************
 **** DESCRIPTORS ****
 *********************)
 
 (*
 * Predicate: DataDescriptor
 *
 * Resource: none
 *
 *)
pred DataDescriptor (d:List) :
	(d == {{ "d", #dval, #dwrit, #denum, #dconf }}) *
	types (#dwrit:Bool, #denum:Bool, #dconf:Bool) * 
	(! (#dval == empty));

 (*
 * Predicate: AccessorDescriptor
 *
 * Resource: none
 *
 *)
pred AccessorDescriptor (d:List) :
	(d == {{ "a", #dget, #dset, #denum, #dconf }}) *
	types (#denum:Bool, #dconf:Bool) * 
	(! (#dget == empty)) * (! (#dset == empty));
	
 (*
 * Predicate: Descriptor
 *
 * Resource: none
 *
 *)
pred Descriptor (d:List) :
	DataDescriptor(d),
	AccessorDescriptor(d);

(*
 * Predicate: GenericDescriptor
 *
 * Resource: none
 *
 *)
pred GenericDescriptor (d:List) :
	(d == {{ "g", #genum, #gconf, #gval, #gwrit, #gget, #gset }});

(*
 * Predicate: Projection functions for descriptors
 *
 * Resource: none
 *
 *)
pred desc_val (d:List, v) :
    v == l-nth(d, 1);

pred desc_writ (d:List, v) :
    v == l-nth(d, 2);

pred desc_get (d:List, v) :
    v == l-nth(d, 1);

pred desc_set (d:List, v) :
    v == l-nth(d, 2);

pred desc_enum (d:List, v) :
    v == l-nth(d, 3);

pred desc_conf (d:List, v) :
    v == l-nth(d, 4);
    
(*********************
 **** OBJECT CELL ****
 *********************) 

(*
 * Predicate: Cell_o(l, prop, X)
 *
 * Resource:
 *      (l, prop)
 *
 *)
pred Cell_o (l:Obj, prop:Str, d) :
    ((l, prop) -> d) * (d == none),
    ((l, prop) -> d) * DataDescriptor(d),
    ((l, prop) -> d) * AccessorDescriptor(d);
    
(***************************
 **** TODO: STRING CELL ****
 ***************************) 




(********************
 **** TO SORT OUT ***
 ********************)	
 
(*
 * Predicate: isClass(c, X)
 *
 * Resource:
 *      (l, "@class")
 *
 *)
pred isClass (c : Str, case : Str) :
	(case == "Array") * (c == "Array"),
	(case == "String") * (c == "String"),
	(case == "Non-Array") * (! (c == "Array")),
	(case == "Non-String") * (! (c == "String"));	

(*
 * The PI predicate
 *)

(*
 * Resource:
 *		(l, "@class")   via isClass
 *		(l, "@proto")	(l, "prop")
 *)
pred Pi (l, prop, lcls, undefined, ls, ltf, lpv) :
    types(l : Obj, prop : Str, #cls : Str, lcls : List, ls : List, ltf : List, lpv : List) *
	((l, "@proto") -> null) * ((l, "@class") -> #cls) *
	isClass (#cls, "Non-String") * Cell_o (l, prop, none) *
    (lcls == {{ #cls }}) * (ls == {{ l }}) * (ltf == {{ false }}) * (lpv == {{ "" }});

(*
 * Resource:
 *		(l, "@class")   via isClass
 *		(l, "@proto")	(l, "prop")
 *)
pred Pi (l, prop, lcls, d, ls, ltf, lpv) :
    types(l : Obj, prop : Str, #cls : Str, lcls : List, d:List, ls : List, ltf : List, lpv : List) *
	((l, "@class") -> #cls) *
	isClass (#cls, "Non-String") * Cell_o (l, prop, d) * (! (d == none)) *
    (lcls == {{ #cls }}) * (ls == {{ l }}) * (ltf == {{ false }}) * (lpv == {{ "" }});

(*
 * Resource:
 *		(l, "@class")   via isClass
 *		(l, "@proto")	(l, "prop")		(l, "@primitiveValue")
 *
 *		Those five fields captured throughout the chain
 *)

pred Pi (l, prop, lcls, d, ls, ltf, lpv) :
    types(l : Obj, prop : Str, lcls : List, ls : List, ltf : List, lpv : List) *
	((l, "@class") -> #cls) * ((l, "@proto") -> #lp) *
	isClass (#cls, "Non-String") * Cell_o (l, prop, none) *
	types(#lcls : List, #lls : List, #ltf  : List, #lpv : List) *
    Pi (#lp, prop, #lcls, d, #lls, #ltf, #lpv) *
	types(#pcls : Str,  #ocls : List,
	        #lp : Obj,  #ols  : List,
		   #ptf : Bool, #otf  : List,
		   #ppv : Str,  #otv  : List) *
	(#lcls == #pcls :: #ocls) * (#lls == #lp :: #ols) * (#ltf == #ptf :: #otf) * (#lpv == #ppv :: #otv) *
	(lcls == #cls :: #lcls) * (ls == l :: #lls) * (ltf == false :: #ltf) * (lpv == "" :: #lpv);