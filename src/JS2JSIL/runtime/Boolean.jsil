import Internals.jsil;

(* ******************* *)
(* THE BOOLEAN LIBRARY *)
(* ******************* *)

(* *** CALL *** *)

proc Boolean_call (xsc, vthis, v) {
	rlab:	xret := "i__toBoolean" (v)
}
with
{
	ret: 	xret, rlab;
};

(* *** CONSTRUCT *** *)

proc Boolean_construct (xsc, vthis, v) {
			pv := "i__toBoolean" (v);

			xmet := new ();
			xret := new (xmet);
			xret := "create_default_object" (xret, $lbool_proto, "Boolean", true);

			m := metadata(xret);
			[m, "@primitiveValue"] := pv;

	rlab:	skip
}
with
{
	ret: 	xret, rlab;
};

(* *** TOSTRING *** *)

proc BP_toString (xsc, vthis) {
			goto [typeOf vthis = Bool] isb notb;

	isb:	xret := vthis;
			goto test;

	notb:	goto [typeOf vthis = Obj] class throw;
	class:	m := metadata(vthis);
			c := [m, "@class"];
			goto [c = "Boolean"] b throw;
	b:		xret := [m, "@primitiveValue"];
	test:	goto [xret = true] rt rf;

	rt:		xret := "true";
			goto rlab;

	rf:		xret := "false";

	rlab:	skip;

	throw:	xret := "TypeError" ();
	elab: 	skip
}
with
{
	ret: 	xret, rlab;
	err: 	xret, elab;
};

(* *** VALUEOF *** *)

proc BP_valueOf (xsc, vthis) {
			goto [typeOf vthis = Bool] isb notb;

	isb:	xret := vthis;
			goto rlab;

	notb:	goto [typeOf vthis = Obj] class throw;
	class:	m := metadata(vthis);
			c := [m, "@class"];
			goto [c = "Boolean"] b throw;
	b:		xret := [m, "@primitiveValue"];

	rlab:	skip;

	throw:	xret := "TypeError" ();
	elab: 	skip
}
with
{
	ret: 	xret, rlab;
	err: 	xret, elab;
};
